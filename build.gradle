plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'java'
    id 'maven-publish'
}

group 'com.mrkirby153'
version '1.0-SNAPSHOT'

java.targetCompatibility = JavaVersion.VERSION_11
java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    compileOnly "net.dv8tion:JDA:5.0.0-alpha.5"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
}
task sourceJar(type: Jar, dependsOn: project.classes) {
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    classifier 'javadoc'
}


String propertyOrEnv(property, env) {
    return project.hasProperty(property) ? project.properties[property] : System.getenv(env)
}

def publishUrl() {
    if (project.version.endsWith('-SNAPSHOT'))
        return "https://repo.mrkirby153.com/repository/maven-snapshots/"
    else
        return "https://repo.mrkirby153.com/repository/maven-releases/"
}

publishing {
    tasks.publish.dependsOn build
    repositories {
        maven {
            name = "mrkirby153"
            url = publishUrl()
            credentials {
                username propertyOrEnv('repositoryUsername', 'REPO_USERNAME')
                password propertyOrEnv('repositoryPassword', 'REPO_PASS')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}
